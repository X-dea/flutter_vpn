group 'io.xdea.flutter_vpn'
version '1.0-SNAPSHOT'

buildscript {
    ext.kotlin_version = '1.6.10'
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:7.1.3'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        // For downloading prebuilt libs
        classpath 'de.undercouch:gradle-download-task:4.1.1'
    }
}

apply plugin: 'de.undercouch.download'

// Download prebuilt native libs.
task downloadNativeLibArchive(type: Download) {
    src 'https://github.com/X-dea/Flutter_VPN/releases/download/v0.11.0/strongswan_libs_v5.9.5.zip'
    dest "$buildDir/native.zip"
}

task getPrebuiltNativeLibs(dependsOn: downloadNativeLibArchive, type: Copy) {
    from zipTree("$buildDir/native.zip")
    into "src/main/libs"
}

// Automatically download native libs before build when doesn't exist.
tasks.whenTaskAdded {
    task ->
        if (task.name == "preBuild" && !(file('./src/main/libs').exists())) {
            preBuild.dependsOn getPrebuiltNativeLibs
        }
}

rootProject.allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion 31

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    sourceSets.main {
        java.srcDirs += 'src/main/kotlin'
        jniLibs.srcDir 'src/main/libs'
    }

    defaultConfig {
        minSdkVersion 16
        consumerProguardFiles 'proguard-rules.pro'
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
}
